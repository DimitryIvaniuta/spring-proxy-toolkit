plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
}

import org.springframework.boot.gradle.plugin.SpringBootPlugin

repositories { mavenCentral() }

dependencies {
    def bootBom = platform(SpringBootPlugin.BOM_COORDINATES)

    // Apply BOM to *all* configs that need version alignment
    implementation bootBom
    testImplementation bootBom
    compileOnly bootBom
    testCompileOnly bootBom
    annotationProcessor bootBom
    testAnnotationProcessor bootBom

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql:11.18.0'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    runtimeOnly 'org.postgresql:postgresql'
    def lombokVersion = '1.18.42'
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:postgresql'
}

tasks.withType(Test).configureEach { useJUnitPlatform() }
